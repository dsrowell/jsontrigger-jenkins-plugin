import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.10.1'
    }
}

apply plugin: 'org.jenkins-ci.jpi'

group = 'org.jenkins-ci.plugins'
version = '0.1'
description = 'Triggers builds based on incoming webhooks'

jenkinsPlugin {
    coreVersion = '1.580.1'

    shortName = 'jsontrigger'
    displayName = 'JSON Trigger Plugin'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/JSON+Trigger+Plugin'
    gitHubUrl = 'https://github.com/dsrowell/jsontrigger-jenkins-plugin'
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

test {
    // Log extra information for the benefit of the Travis CI build log
    testLogging {
        // Print more verbose stacktraces
        exceptionFormat 'full'

        // Plugin logging and stacktraces from Jenkins are printed to stderr
        events 'passed', 'skipped', 'failed', 'standardError'

        // Jenkins prints 100MB+ of incredibly verbose low-level logs to stdout;
        // this is not useful, and causes Travis CI to fail as it only allows for 4MB of output
        if (System.env['LOG_STDOUT'] != null) {
            events << TestLogEvent.STANDARD_OUT
        }
    }
}
